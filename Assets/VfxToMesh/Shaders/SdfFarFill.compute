#pragma kernel FillSdfFar

#include "UnityCG.cginc"

RWTexture3D<float> _DestSdf;
Texture3D<float> _SourceSdf;

int _GridResolution;
float _SdfFar;
float _VoxelSize;

static const int3 NEIGHBORS[6] = {
    int3(1, 0, 0),
    int3(-1, 0, 0),
    int3(0, 1, 0),
    int3(0, -1, 0),
    int3(0, 0, 1),
    int3(0, 0, -1),
};

[numthreads(8, 8, 8)]
void FillSdfFar(uint3 id : SV_DispatchThreadID)
{
    if (any(id >= _GridResolution))
        return;

    float current = _SourceSdf.Load(int4(id, 0));
    if (current < _SdfFar - 0.0001f)
    {
        _DestSdf[id.xyz] = current;
        return;
    }

    float best = _SdfFar;
    for (int i = 0; i < 6; ++i)
    {
        int3 neighbor = clamp(int3(id) + NEIGHBORS[i], 0, _GridResolution - 1);
        float sample = _SourceSdf.Load(int4(neighbor, 0));
        if (sample < _SdfFar - 0.0001f)
        {
            best = min(best, sample + _VoxelSize);
        }
    }

    _DestSdf[id.xyz] = best;
}
