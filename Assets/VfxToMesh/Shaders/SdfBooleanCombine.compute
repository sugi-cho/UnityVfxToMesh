#pragma kernel BlendSdf

#define THREADS_3D 8

cbuffer BooleanParams : register(b0)
{
    int _GridResolution;
    int _Operation;
    float _SmoothBeta;
};

Texture3D<float> _InputA;
Texture3D<float> _InputB;
RWTexture3D<float> _Output;

float smax_lse2(float a, float b, float beta)
{
    float m = max(a, b);
    float ex = exp(beta * (a - m)) + exp(beta * (b - m));
    return m + log(ex) / beta;
}

[numthreads(THREADS_3D, THREADS_3D, THREADS_3D)]
void BlendSdf(uint3 id : SV_DispatchThreadID)
{
    if (any(id >= uint3(_GridResolution, _GridResolution, _GridResolution)))
    {
        return;
    }

    float a = _InputA.Load(int4(id, 0));
    float b = _InputB.Load(int4(id, 0));
    float result = a;
    if (_Operation == 0)
    {
        result = min(a, b);
    }
    else if (_Operation == 1)
    {
        float beta = max(0.01, _SmoothBeta);
        result = smax_lse2(a, b, beta);
    }
    else if (_Operation == 2)
    {
        result = max(a, -b);
    }

    _Output[id] = result;
}
